buildscript {
    ext {
        asciiDocOutputDir = file("${buildDir}/asciidoc/generated")
        swaggerOutputDir = file("${buildDir}/swagger")
        snippetsOutputDir = file("${buildDir}/asciidoc/snippets")
        springfoxVersion = '2.7.0'
        swaggerAnnotationsVersion = '1.5.16'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("io.github.swagger2markup:swagger2markup-spring-restdocs-ext:1.2.0")
        classpath("io.github.swagger2markup:swagger2markup-gradle-plugin:1.2.0")
        classpath("org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.10.1")
    }
}

// new plugins syntax
plugins {
    // code
    id "java"
    // dependency management
    id 'com.github.ben-manes.versions' version '0.28.0' // find new versions of dependency task: dependencyUpdates
    // spring
    id 'org.springframework.boot' version '2.2.7.RELEASE'
    // code quality
    id "jacoco"
    id "org.owasp.dependencycheck" version "5.3.2.1"
    // IDEs
    id "eclipse"
    id "idea"
    // publishing
    id "com.cinnober.gradle.semver-git" version "2.4.0"
    id "maven-publish"
    // documentation
    id "org.asciidoctor.convert" version "1.5.3"
}

// spring boot
apply plugin: 'io.spring.dependency-management'

// documentation
apply plugin: 'io.github.swagger2markup'

sourceCompatibility = 1.8
targetCompatibility = 1.8

project.group = 'eu.h2020.symbiote'

// dependencies section
repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}
// Spring related configs
ext {
	set('springCloudVersion', 'Hoxton.SR4')
}
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

/*
Forces dependencies' cache invalidation for each build for dependencies marked with
    { changing = true }
    e.g.
    compile('com.github.symbiote-h2020:SymbIoTeSecurity:develop-SNAPSHOT'){ changing = true }
 */
configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    exclude group: 'org.slf4j', module : "slf4j-log4j12"
}

dependencies {
    /* SymbIoTeSecurity manual:
        One can use the library from jitpack by default with the notation:
            compile('com.github.symbiote-h2020:SymbIoTeSecurity:develop-SNAPSHOT') {changing = true }
        or having cloned locally the SymbIoTeLibraries repo use it directly with project dependency by:
            compile project(':SymbIoTeSecurity')
        Important --- In order to use the latter you need to:
            1) switch comments on the artifact and project dependencies
            2) always have only one uncommented
            3) project dependency requires changes in settings.gradle file in this project
            4) never commit build.gradle which has project dependencies active as it will break CI builds
    */
    //compile('com.github.symbiote-h2020:SymbIoTeSecurity:[27.2, 27.999]'){ changing = true }
    // allows to select the downloaded matching sources in IntelliJ Idea as it fails to do so automatically from jitpack repo
    //compile('com.github.symbiote-h2020:SymbIoTeSecurity:[27.2, 27.999]:sources'){ changing = true }
    //compile project(':SymbIoTeSecurity')
    compile('com.github.symbiote-h2020:SymbIoTeSecurity:[28.0, 28.999]') {changing = true }
    //compile('org.bouncycastle:bcprov-jdk15on:1.60')
    //compile('org.bouncycastle:bcpkix-jdk15on:1.60')
    

    compile('com.github.symbiote-h2020:SymbIoTeLibraries:[6.0, 6.999]'){ changing = true }
    //compile project(':SymbIoTeLibraries')

    // Spring
    compile('org.springframework.cloud:spring-cloud-starter')
    compile('org.springframework.cloud:spring-cloud-starter-config')
    compile('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client')
    compile('org.springframework.cloud:spring-cloud-starter-zipkin')
    compile('org.springframework.retry:spring-retry')
    compile('org.springframework.boot:spring-boot-starter-amqp')
    compile('org.springframework.boot:spring-boot-starter-aop')
    compile('org.springframework.boot:spring-boot-starter-cache')
    compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    compile('org.springframework.boot:spring-boot-starter-web')

	runtime("org.springframework.boot:spring-boot-properties-migrator")

    // JDK 9 runtime fixes
    if(JavaVersion.current() == JavaVersion.VERSION_1_9){
        runtime('javax.xml.bind:jaxb-api:+')
    }

    // Swagger annotations for documentation
    compile("io.swagger:swagger-annotations:${swaggerAnnotationsVersion}")

    // tests only
    testCompile('junit:junit:[4.12, 4.999]')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    //testCompile('com.github.fakemongo:fongo:2.2.0-RC2')
    testCompile('de.flapdoodle.embed:de.flapdoodle.embed.mongo')
    testCompile("io.springfox:springfox-swagger2:${springfoxVersion}")
    testCompile("io.springfox:springfox-bean-validators:${springfoxVersion}")
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
}

// publication
bootJar {
	classifier = 'run'
}

task generateJavaDocs(type: Javadoc) {
    source = sourceSets.main.allJava
    destinationDir = reporting.file("javadocs")
}

task javadocJar(type: Jar, dependsOn: generateJavaDocs) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            version = project.version
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            //artifact javadocJar

            artifact(file("$libsDir/$project.name-$project.version-${bootJar.classifier}.jar")) {
                classifier "run"
            }
        }
    }
}

publishToMavenLocal.dependsOn(bootJar)

// code quality below
// jacoco configuration section
jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

// owasp config
dependencyCheck {
    outputDirectory = "build/reports/security"
}

// including code quality extensions into the build
check.dependsOn(jacocoTestReport)//,'dependencyCheckAnalyze')

test {
    // we don't need that for testing,
    exclude '**/Swagger*'

    testLogging {
        exceptionFormat = 'full'
    }
}

// documentation below
task swaggerGeneratorTest(type: Test, dependsOn: testClasses) {
    include '**/Swagger*'
    systemProperty 'io.springfox.staticdocs.outputDir', swaggerOutputDir
    systemProperty 'io.springfox.staticdocs.snippetsOutputDir', snippetsOutputDir
}

convertSwagger2markup {
    dependsOn swaggerGeneratorTest
    swaggerInput "${swaggerOutputDir}/swagger.json"
    outputDir asciiDocOutputDir
    config = [
            'swagger2markup.pathsGroupedBy' : 'TAGS',
            'swagger2markup.extensions.springRestDocs.snippetBaseUri': snippetsOutputDir.getAbsolutePath()]
}

asciidoctor {
    dependsOn convertSwagger2markup
    sources {
        include 'index.adoc'
    }
    backends = ['html5', 'pdf']
    attributes = [
            doctype: 'book',
            toc: 'left',
            toclevels: '3',
            numbered: '',
            sectlinks: '',
            sectanchors: '',
            hardbreaks: '',
            generated: asciiDocOutputDir
    ]
}
